// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

	 field Array str;
	 field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
    	if (maxLength = 0) { let maxLength = maxLength + 1; }
    	let str = Array.new(maxLength);
    	let length = 0;
    	return this;
    }

    /** Disposes this string. */
    method void dispose() {
    	do Memory.deAlloc(str);
    	return;
    }

    /** Returns the current length of this string. */
    method int length() {
    	return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
    	return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
    	let str[j] = c;
    	return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
    	let str[length] = c;
    	let length = length + 1;
    	return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
    	let length = length - 1;
    	return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
    	var int digit, i;
    	var boolean negative;
    	// handle minus sign
    	if ( str[i] = 45 ) { let negative = true; let i = i + 1; }
    	while (i < length) {
    		// str[i] not a digit
    		if ((str[i] > 57) | (str[i] < 48)) { 
    			let i = length;
    		} else {
    			let digit = (digit * 10) + (str[i] - 48);
    			let i = i + 1;
			}
    	}
    	if (negative) { let digit = -digit; }
    	return digit;
    }
    
    /** Wrapper method for handling signedness. */
    method void setInt(int val) {
    	let length = 0;	// reset string
    	if (val < 0) { do appendChar(45); }	
    	do setSigned(Math.abs(val));
    	return;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setSigned(int val) {
    	var char lastDigit, c;
    	let lastDigit = Math.mod(val, 10);
		let c = lastDigit + 48;
    	if (val < 10) { 
    		do appendChar(c);
    		return;
     	} else {
    		do setSigned(val / 10);
    		do appendChar(c);
    		return;
    	}
    }

    /** Returns the new line character. */
    function char newLine() {
    	return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
    	return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
    	return 34;
    }
}
